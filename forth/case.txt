\ *********************************************************************
\    Case for FlashForth                                              *
\    Filename:      case.txt                                          *
\    Date:          26.01.2014                                        *
\    FF Version:    5.0                                               *
\    Copyright:     Mikael Nordman                                    *
\    Author:        Mikael Nordman                                    *
\ *********************************************************************
\    FlashForth is licensed acording to the GNU General Public License*
\ *********************************************************************
\ A case implementation posted by Jenny Brien on c.l.f.
\ Modified to use for..next instead of do..loop

-case
marker -case
hex ram

ram variable #of  \ Number of branches to resolve

: (of)  \ Speed and size optimization of case
  inline over
  inline -
  0=
;

: case  (  x -- x #of )
  #of @  0 #of !  \ allow nesting
; immediate

: of  ( -- orig )  
    postpone (of)  ( copy and test case value)
    postpone if    ( add orig to control flow stack )
    postpone drop  ( discard case value if case is matching )
; immediate

: default ( -- orig )
  postpone true      ( Force to take the default branch )
  postpone if        ( add orig to control flow stack )
  postpone drop      ( discard case value )
; immediate

: endof ( orig1  -- orig2 )
   postpone else 1 #of +!
; immediate

: endcase  ( #of  orig1..orign  -- )
    postpone drop  ( discard case value )
    #of @ for
      postpone then
    next
    #of !
; immediate
