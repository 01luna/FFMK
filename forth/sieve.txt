\ *******************************************************************
\                                                                   *
\    Filename:      sieve.txt                                       *
\    Date:          31.12.2013                                      *
\    FF Version:    5.0                                             *
\    MCU:           PIC 18 24 30 33 Atmega                          *
\    Copyright:     Mikael Nordman                                  *
\    Author:        Mikael Nordman                                  *
\ *******************************************************************
\ FlashForth is licensed according to the GNU General Public License*
\ *******************************************************************
\ This normal sieve requires 8 KBytes of RAM.
\ It will not run on most PICs due to lack of memory.
\ sieve2 requires 1 Kbyte of RAM.

-sieve
marker -sieve
decimal ram
8191 constant size inlined
ram align here size allot constant flags inlined
: sieve
  flags size 1 fill
  0 1 !p>r size 1-
  for
    flags @p + c@
    if                              
        @p dup + 3 + 
        dup @p +
        begin
           dup size  <
        while
           0 over flags + c!
           over +
        repeat
        drop drop 1+ 
     then
     p+
  next
  r>p
  . ." primes " cr ;

: bench ticks sieve ticks swap - u. ." milliseconds" cr ;

-sieve2
marker -sieve2
decimal ram
8192 constant size2
variable flags2 size2 8 / allot
: bit-addr ( addr bit -- eff-addr )
  3 rshift  ( -- addr off)
  +         ( -- eff-addr) ;

: bit? ( addr bit -- f )
  swap over bit-addr swap ( -- eff-addr bit )
  7 and 1 swap lshift     ( -- eff-addr bitmask)
  swap c@ and             ( -- f) ;

: bit-reset ( addr bit -- )
  swap over bit-addr swap ( -- eff-addr bit )
  7 and 1 swap lshift     ( -- eff-addr bitmask)
  invert over c@ and swap c! ;

: sieve2      
  flags2 [ size2 8 / ] literal -1 fill
  0 0 !p>r size2 
  for 
     flags2 @p bit? 
     if 
        @p 2*  3 +
        dup  @p +
        begin  
          dup size2 u< 
        while  
          flags2 over bit-reset
          over +
        repeat
        2drop 1+
     then
     p+
  next
  r>p   . ." primes " cr
;

: bench2 ticks sieve2 ticks swap - u. ." milliseconds" cr ;



