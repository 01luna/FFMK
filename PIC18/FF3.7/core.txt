\ *********************************************************************
\                                                                     *
\    Filename:      core.txt                                          *
\    Date:          14.2.2010                                         *
\    FF Version:    3.4                                               *
\    Copyright:     Mikael Nordman                                    *
\    Author:        Mikael Nordman                                    *
\ *********************************************************************
\    FlashForth is licensed acording to the GNU General Public License*
\ *********************************************************************
\ Some extra core words

-core
marker -core
hex ram

\ Interpret a string. The string must be in ram
: evaluate ( caddr n -- )
  'source 2@ >r >r >in @ >r
  interpret
  r> >in ! r> r> 'source 2!
;

: forget ( --- name )
  bl word latest @ (f) abort" ?"
  c>n 2- dup @ abort" ?"
  dup flash dp ! @ latest ! ram
;

: fill ( addr n c -- ) \ fill addr to addr+n with c
  rot !p>r swap
  for
    dup pc! p+
  next
  r>p drop 
;
: erase  ( addr n -- )
  0 fill
;

: blanks  ( addr n -- )
  bl fill
;

: ?dup ( x -- 0 | x x )
  dup if inline dup then 
;

: pick ( xu ... x0 u -- xu ... x0 xu) 
  2* 3 + sp@ swap - @
;

: in? ( nfa -- flag )
  c@ $40 and
;
hex ram

