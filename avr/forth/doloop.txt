\ *********************************************************************
\                                                                     *
\    Filename:      doloop.txt                                        *
\    Date:          19.01.2014                                        *
\    File Version:  5.0                                               *
\    MCU:           Atmega (not 256)                                  *
\    Copyright:     Mikael Nordman                                    *
\    Author:        Mikael Nordman                                    *
\ *********************************************************************
\    FlashForth is licensed acording to the GNU General Public License*
\ *********************************************************************
\ do loop for Atmega32,64,128 (not 256)
-doloop
marker -doloop
: compileonly $10 shb ;
ram variable ind
ram variable lim
: (do)  ( limit index -- R: leave oldindex oldlim ) 
  r>
  dup xa> @ >r
  ind @ >r lim @ >r
  1+ >r
  ind ! lim !
; compileonly

: (?do)
  2dup xor
  if
    [ '  (do) ] again  \ branch to (do) 
  then
  r> xa> @ >r 2drop
;
: (loop)
  ind @ 1+ dup ind !
  lim @ =
; compileonly

: (+loop) ( n -- )
  dup >r            \ remember increment
  ind @ + dup ind !
  lim @ r> 0< 
  if       <
  else  1- >
  then            
; compileonly

: (unloop)
  r>
  r> lim ! r> ind ! rdrop
  >r
; compileonly

: do
  postpone (do)
  postpone begin
  flash 2 allot ram
  postpone begin
; immediate compileonly

: ?do
  postpone (?do)
  postpone begin
  flash 2 allot ram
  postpone begin
; immediate compileonly

: leave
  rdrop r> lim ! r> ind ! 
; compileonly

: i [ ind literal ] @ ; compileonly

: j r> r> r> dup >r swap >r swap >r ; compileonly

: loop
  postpone (loop)
  postpone until
  postpone (unloop)
  flash here >xa swap ! ram
; immediate compileonly

: unloop
  postpone (unloop)
; immediate compileonly

: +loop
  postpone (+loop)
  postpone until
  postpone (unloop)
  flash here >xa swap ! ram
; immediate compileonly

