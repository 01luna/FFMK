\ *******************************************************************
\                                                                   *
\    Filename:      task-test.txt                                   *
\    Date:          28.05.2011                                      *
\    FF Version:    4.7                                             *
\    MCU:           PIC 24 33                                       *
\    Copyright:     Mikael Nordman                                  *
\    Author:        Mikael Nordman                                  *
\ *******************************************************************
\ FlashForth is licensed according to the GNU General Public License*
\ *******************************************************************
\ It seems a nop is required between two consecutive
\ read modify instructions on PORTB
single
-t1
marker -t1
ram hex
\ Registers for dsPIC33FJ128GP802. Change if needed.
$032c con adpcfg
$02cc con latb
$02ca con portb
$02c8 con trisb
ram variable delay
: led9off  [ #9 portb bclr,  ] ; inlined
: led9on   [ #9 portb bset,  ] ; inlined
: led10off [ #10 portb bclr, ] ; inlined
: led10on  [ #10 portb bset, ] ; inlined

40 30 30 0 task: t1
: tloop 
  $3f adpcfg c!
  100 delay !
  [ #9 trisb bclr, ] 
  [ #10 trisb bclr, ] 
  begin 
    delay @ ms 
    [ #10 portb btst,   ]
    [ z, if,          ]
        led9off
        [ flash 0 , ram ] \ nop required 
        led10on
    [ else,           ] 
        led9on
        [ flash 0 , ram ] \ nop required
        led10off
    [ then,           ]
  again
;

: t1go ['] tloop t1 tinit t1 run ;

' t1go is turnkey
warm

