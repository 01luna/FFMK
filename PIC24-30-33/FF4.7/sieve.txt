-sieve
marker -sieve
decimal ram
8191 con size inlined
ram here size allot con flags inlined
: sieve
  flags size 1 fill
  0 1 !p>r size 1-
  for
    flags @p + c@
    if                              
        @p dup + 3 + 
        dup @p +
        begin
           dup size  <
        while
           0 over flags + c!
           over +
        repeat
        drop drop 1+ 
     then
     p+
  next
  r>p
  . ." primes " cr ;

: bench ticks swap for sieve next ticks swap - u. ." milliseconds" cr ;

-sieve2
marker -sieve2
decimal ram
8192 constant size2
variable flags2 size2 8 / allot

: bit-addr ( addr bit -- eff-addr )
  3 rshift  ( -- addr off)
  +         ( -- eff-addr) ;

: bit? ( addr bit -- f )
  swap over bit-addr swap ( -- eff-addr bit )
  7 and 1 swap lshift     ( -- eff-addr bitmask)
  swap c@ and             ( -- f) ;

: bit-reset ( addr bit -- )
  swap over bit-addr swap ( -- eff-addr bit )
  7 and 1 swap lshift     ( -- eff-addr bitmask)
  invert over c@ and swap c! ;

: sieve2      
  flags2 [ size2 8 / ] literal -1 fill
  0 0 !p>r size2 
  for 
     flags2 @p bit? 
     if 
        @p 2*  3 +
        dup  @p +
        begin  
          dup size2 u< 
        while  
          flags2 over bit-reset
          over +
        repeat
        2drop 1+
     then
     p+
  next
  r>p   . ." primes " cr
;

: bench2 ticks swap for sieve2 next ticks swap - u. ." milliseconds" cr ;


-sieve3
marker -sieve3
decimal ram
8192 con size3 inlined
\ variable flags3 size3 8 / allot
ram align here size3 8 / allot con flags3 inlined

: sieve3      
  flags3 [ size3 8 / ] literal -1 fill
  0 0 !p>r size3 
  for 
     flags3 @p btst 
     if 
        @p 2*  3 +
        dup  @p +
        begin  
          dup size3 u< 
        while  
          flags3 over bclr
          over +
        repeat
        2drop 1+
     then
     p+
  next
  r>p   . ." primes " cr
;

: bench3 ticks swap for sieve3 next ticks swap - u. ." milliseconds" cr ;

