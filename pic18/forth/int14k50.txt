\ *********************************************************************
\    Interrupts example for FlashForth                                *
\    Filename:      int14k50.txt                                      *
\    Date:          11.12.2019                                        *
\    FF Version:    5.0                                               *
\    MCU:           PIC18                                             *
\    Copyright:     Mikael Nordman                                    *
\    Author:        Mikael Nordman                                    *
\ *********************************************************************
\    FlashForth is licensed acording to the GNU General Public License*
\ *********************************************************************
\ NOTE: Always deactivate user interrupts before
\ the interrupt word is removed.
\ You must also clear any related interrupt enable bits 
\ and interrupt flags before zeroing the interrupt vector
\ NOTE: Interrupt vector 0 is the high prority interrupt vector.
\ NOTE: The low priority interrupt vector is not implemented.

ccp1.int.off
-user.int
marker -user.int

$ff94 constant trisc
$ff8b constant latc
$ffb1 constant t3con
$ffbd constant ccp1con
$ffbe constant ccpr1
$ff9e constant pir1
$ff9d constant pie1
$0004 constant ccp1if
$0004 constant ccp1ie

ram variable ccp1/edge
ram variable width.clocks
ram variable width.us
ram variable ccp1.int.cnt

: ccp1.init
  0 ccp1con c!         \ reset capture module
  5 ccp1con c!         \ capture rising edge
  %10111001 t3con c!   \ timer3 as capture counter prescaler=8
  %00100000 trisc mclr \ ccp1 input port configured as output
  \ %00100000 trisc mset \ ccp1 input port configured as input                     
;

: ccp1.int
  [i
  ccp1if pir1 mtst if
    1 ccp1.int.cnt +!
    ccp1if pir1 mclr
    ccp1con c@ 5 = if
      4 ccp1con c!
      ccpr1 @ ccp1/edge !
    else
      5 ccp1con c!
      ccpr1 @ ccp1/edge @ - width.clocks !
    then
  then
  i]
;i

: pulsewidth 
  width.clocks @ #1000 Fcy 8 /  u*/mod nip u. ." us"
;

: ccp1.int.init
  0 ccp1.int.cnt !
  ccp1.init
  ['] ccp1.int 0 int!
  ccp1ie pie1 mset
; 

: ccp1.int.off
  0 0 int!
  ccp1ie pie1 mclr
;

\ TESTS
\ tests require that ccp1 input port is configured as output
\ see ccp1.init
: test1 10 ms $20 latc mset 1 ms  $20 latc mclr pulsewidth ;
: test2 10 ms $20 latc mset 2 ms  $20 latc mclr pulsewidth ;
: test3 10 ms $20 latc mset 3 ms  $20 latc mclr pulsewidth ;

ccp1.int.init
test1
test2
test3
ccp1.int.cnt @ u.
